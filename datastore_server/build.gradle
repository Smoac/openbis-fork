evaluationDependsOn(':commonbase')
evaluationDependsOn(':common')
evaluationDependsOn(':openbis_api')
evaluationDependsOn(':openbis-common')
evaluationDependsOn(':authentication')
evaluationDependsOn(':dbmigration')
evaluationDependsOn(':openbis')

apply from: '../gradle/javaproject.gradle'

configurations.create('testRuntimeFirst')
configurations.create('testRuntimeSecond')

dependencies {

	compile (project(':openbis')) {
		exclude group: 'google', module: 'gwt-user'
	}

	compile project(':common'),
			project(':openbis-common'),
			// authentication and dbmigration are required as they contain later versions of 
			// some classes than cisd-cifex.jar. They need to be in classpath before cisd-cifex.jar
			project(':authentication'),
			project(':dbmigration'),
			'apache:ftpserver-core:+',
			'apache:sshd-core:+',
			'jfree:jfreechart:+',
			'cisd:cisd-cifex:+',
			'shriop:javacsv:+',
			'cisd:cisd-image-readers:+',
			'hjg:pngj:+',
			'apache:commons-fileupload:+',
			'lyncode:xoai-data-provider:+'

  runtime 'bioformats:bioformats:+'

	testCompile (project(path: ':openbis', configuration: 'tests')) {
		exclude group: 'google', module: 'gwt-user'
	}
				
	testCompile project(path: ':common', configuration: 'tests'),
				project(path: ':openbis-common', configuration: 'tests')

	testRuntimeFirst 'javax:servlet-api:+'
	testRuntimeSecond 'google:gwt-user:+'
}

configurations {
	testRuntime.exclude group: 'google', module: 'gwt-servlet'
}

sourceSets.test.runtimeClasspath = configurations.testRuntimeFirst + configurations.testRuntimeSecond + sourceSets.test.runtimeClasspath

sourceSets { 
	test {
		resources {
			srcDirs = ['source/java']
		}
	}
}

jar {
    from('targets/dist') {
        include 'BUILD*INFO'
    }
    from('source/java') {
        include '*.xml'
    }
    from('source') {
        include 'sql/**/*.sql'
        into 'datastore_server'
    }
}
