buildscript {
  apply from: '../gradle/repository.gradle'

  repositories {
    ivy {
      ivyPattern "https://sissource.ethz.ch/openbis/openbis-public/openbis-ivy/-/raw/main/[organisation]/[module]/[revision]/ivy.xml"
      artifactPattern "https://sissource.ethz.ch/openbis/openbis-public/openbis-ivy/-/raw/main/[organisation]/[module]/[revision]/[artifact]-[revision](-[classifier]).[ext]"
    }
  }

  dependencies {
    classpath 'com.fasterxml.jackson.core:jackson-core:2.13.3',
      'com.fasterxml.jackson.core:jackson-databind:2.13.3',
      'com.github.node-gradle:gradle-node-plugin:3.2.1'
  }
}

configure(allprojects) {
  apply plugin:'base'

  repositories {
    mavenCentral()
  }
}

apply plugin: "com.github.node-gradle.node"

node {
  download = true
  version = '10.22.0'
  workDir = file("${projectDir}/node/nodejs")
  nodeModulesDir = file("${projectDir}")
}

task cleanBuild(type:Delete){
  delete 'build'
}

task copyCorePlugins(type: Copy) {
  from file('src/core-plugins')
  into file('build/core-plugins')
}

task copyWebApp(type: Copy) {
  from file('build/js')
  into file('build/core-plugins/openbis-ng-ui/1/as/webapps/openbis-ng-ui/html')
}

task test {
  dependsOn npm_run_test
}

npm_run_build.dependsOn cleanBuild
copyCorePlugins.dependsOn npm_run_build
copyWebApp.dependsOn copyCorePlugins
build.dependsOn copyWebApp
build.dependsOn test