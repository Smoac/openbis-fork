evaluationDependsOn(':commonbase')
evaluationDependsOn(':common')
evaluationDependsOn(':openbis_api')
evaluationDependsOn(':openbis-common')
evaluationDependsOn(':authentication')
evaluationDependsOn(':dbmigration')
evaluationDependsOn(':openbis')
evaluationDependsOn(':datastore_server')

apply from: '../gradle/javaproject.gradle'

archivesBaseName = 'datastore_server_plugin-plasmid'

dependencies {
    api project(':datastore_server'),
    		'openbis:openbis-mobile:r29271'
            
    testImplementation project(path: ':commonbase', configuration: 'tests'),
                       project(path: ':common', configuration: 'tests'),
                       project(path: ':dbmigration', configuration: 'tests'),
                       project(path: ':authentication', configuration: 'tests'),
                       project(path: ':openbis_api', configuration: 'tests'),
                       project(path: ':openbis-common', configuration: 'tests'),
                       project(path: ':datastore_server', configuration: 'tests'),
                       'fjelmer:classycle:1.4.2',
                       'testng:testng:6.8-CISD'
    testImplementation (project(path: ':openbis', configuration: 'tests')) {
        exclude group: 'google', module: 'gwt-user'
    }
}

sourceSets {
    main {
        resources {
            srcDirs = ['source/java']
        }
    }
    test {
        resources {
            srcDirs = ['source/java']
        }
    }
}

jar {
    duplicatesStrategy 'include'
    manifest {
        attributes("Main-Class": "ch.systemsx.cisd.plasmid.BuildAndEnvironmentInfo",
                   "Version": versionNumber,
                   "Build-Number": "${versionNumber} (r${revisionNumber},${cleanFlag})")
    }
    
    from('targets/dist') {
        include 'BUILD-*'
    }
}

task plasmidZip(type: Zip) {
	baseName 'datastore_server_plugin-plasmid'
	from (jar.archivePath) {
		into 'datastore_server/lib'
	}
    from('source/core-plugins') {
		into 'core-plugins'
    }
    rename 'datastore_server_plugin-plasmid(.*)\\.jar', 'datastore_server_plugin-plasmid.jar'
}
build.dependsOn plasmidZip
