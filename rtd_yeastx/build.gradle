evaluationDependsOn(':commonbase')
evaluationDependsOn(':common')
evaluationDependsOn(':openbis_api')
evaluationDependsOn(':openbis-common')
evaluationDependsOn(':authentication')
evaluationDependsOn(':dbmigration')
evaluationDependsOn(':openbis')
evaluationDependsOn(':datastore_server')

apply from: '../gradle/javaproject.gradle'

configurations {
    distribution
}

dependencies {
    api project(':datastore_server')
    
    testImplementation project(path: ':commonbase', configuration: 'tests'),
                       project(path: ':common', configuration: 'tests'),
                       project(path: ':dbmigration', configuration: 'tests'),
                       project(path: ':authentication', configuration: 'tests'),
                       project(path: ':openbis_api', configuration: 'tests'),
                       project(path: ':openbis-common', configuration: 'tests'),
                       project(path: ':datastore_server', configuration: 'tests'),
                       'fjelmer:classycle:1.4.2',
                       'testng:testng:6.8-CISD'
    testImplementation (project(path: ':openbis', configuration: 'tests')) {
        exclude group: 'google', module: 'gwt-user'
    }
    distribution 'jfree:jfreechart:1.0.13'
}

sourceSets { 
    main {
        resources {
            srcDirs = ['source/java', 'source']
            exclude 'java/**'
        }
    }
}

jar {
    duplicatesStrategy 'include'
    manifest {
        attributes("Main-Class": "ch.systemsx.cisd.yeastx.BuildAndEnvironmentInfo",
                   "Version": versionNumber,
                   "Build-Number": "${versionNumber} (r${revisionNumber},${cleanFlag})")
    }
    from('targets/dist') {
    	include 'BUILD-*'
    }
}

task zip(type: Zip) {
    from jar
    from configurations.distribution
    into 'datastore_server/lib/'
    rename "datastore_server_plugin-yeastx.*\\.jar", "datastore_server_plugin-yeastx.jar"
}

zip.dependsOn jar

build.dependsOn zip

archivesBaseName = 'datastore_server_plugin-yeastx'


