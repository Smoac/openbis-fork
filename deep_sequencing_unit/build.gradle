evaluationDependsOn(':commonbase')
evaluationDependsOn(':common')
evaluationDependsOn(':openbis_api')
evaluationDependsOn(':openbis-common')
evaluationDependsOn(':authentication')
evaluationDependsOn(':dbmigration')
evaluationDependsOn(':openbis')
evaluationDependsOn(':datastore_server')

apply from: '../gradle/javaproject.gradle'

archivesBaseName = 'datastore_server_plugin-dsu'

configurations.create('trackingClient')
configurations.create('trackingClientPartial')
dependencies {
	compile project(':common'),
            project(':openbis-common'),
            project(':openbis_api'),
            project(':openbis'),
            project(':datastore_server'),
            'apache:commons-cli:1.2'
            

    testCompile project(path: ':datastore_server', configuration: 'tests')
    
    trackingClient 'sis:sis-base:18.09.0',
                    'javax:activation:1.1.1',
                    'javax:mail:1.4.3',
                    'apache:log4j:1.2.15',
                    'springframework:spring-aop:5.0.1.RELEASE',
                    'springframework:spring-beans:5.0.1.RELEASE',
                    'springframework:spring-context:5.0.1.RELEASE',
                    'springframework:spring-core:5.0.1.RELEASE',
                    'springframework:spring-web:5.0.1.RELEASE',
                    'springframework:spring-webmvc:5.0.1.RELEASE',
                    'springframework:spring-expression:5.0.1.RELEASE',
                    'marathon:marathon-spring-util:1.2.5',
                    'apache:httpclient:4.3.6',
                    'apache:httpcore:4.3.3',
                    'apache:commons-logging:1.2',
                    'aopalliance:aopalliance:1.0',
                    'apache:commons-cli:1.2'

    trackingClientPartial 'google:gwt-user:2.4'
}

task trackingJar(type: Jar) {
    archiveName "openbis-tracking-qgf-client.jar"

    from zipTree(project(':common').jar.archivePath).plus(
         zipTree(project(':openbis-common').jar.archivePath).plus(
         zipTree(project(':commonbase').jar.archivePath).plus( 
         zipTree(project(':openbis_api').jar.archivePath).plus( 
         zipTree(project(':datastore_server').jar.archivePath).plus( 
         zipTree(project(':openbis').jar.archivePath)))))).matching {
            include '**/*.class'
    }

    from zipTree(configurations.trackingClientPartial.files.iterator().next()).matching {
        include '**/com/google/gwt/user/client/rpc/IsSerializable.class'
        include '**/com/google/gwt/user/client/rpc/SerializableException.class'
    }
    
    from (sourceSets.main.output.classesDir) {
        include '**/*'
    }    

    manifest {
        attributes 'Main-Class': 'ch.ethz.bsse.cisd.dsu.tracking.main.TrackingClient',
                   "Class-Path": configurations.trackingClient.files.collect { it.getName() }.join(' ')
    }
}

task trackingZip(type: Zip, dependsOn: [trackingJar]) {
    baseName = "openbis-tracking-qgf-client"

    from (trackingJar.archivePath) {
        into "tracking-${project.version}/lib"
    }

    from (configurations.trackingClient.files) {
        into "tracking-${project.version}/lib"
    }
    
    from ('tracking/dist/etc') {
        into "tracking-${project.version}/etc"
    }

    from ('tracking/dist/tracking.sh') {
        into "tracking-${project.version}/"
    }
}

task zip(type: Zip) {
	includeEmptyDirs false
    from (jar.archivePath) {
	    into 'datastore_server/lib/'
	    rename 'datastore_server_plugin-dsu(.*)\\.jar', 'datastore_server_plugin-dsu.jar'
	}
    
    from (fileTree(dir: 'source/core-plugins', includes:['illumina-ngs/**'], excludes:['**/as/**', '**/package-to-dist'])) {
		into 'core-plugins'    
	}
}

zip.dependsOn jar
build.dependsOn zip
build.dependsOn trackingZip