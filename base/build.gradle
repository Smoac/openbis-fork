apply from: 'http://svncisd.ethz.ch/repos/cisd/gradle/trunk/javaproject.gradle'
apply plugin: 'ivy-publish'

dependencies {
	compile 'apache:commons-io:1.4',
			'apache:commons-lang:2.4',
			'testng:testng:6.8-CISD',
			'rinn:restrictions:1.0.1',
			'cisd:cisd-unix:r15645',
			'cisd:cisd-nativedata:r27382'
} 


group="cisd"
new ByteArrayOutputStream().withStream { os ->
    def result = exec {
        executable = 'svn'
        args = ['info']
        standardOutput = os
    }
    def outputAsString = os.toString()
    def matchLastChangedRevMatcher = outputAsString =~ /Last Changed Rev: (\d+)/
    def urlMatcher = outputAsString =~ /URL: .*\/(.+)/

	def revision = "r${matchLastChangedRevMatcher[0][1]}"
	def branch = urlMatcher[0][1]
	
	if (branch.equals('trunk')) {
		project.version = revision
	} else {
		project.version = branch
	}
}

publishing {
    publications {
    	ivy(IvyPublication) {
			from components.java
			artifact(sourceJar)
		}
    }
    repositories {
    	ivy {
			ivyPattern "file://$buildDir/../../ivy-repository/[organisation]/[module]/[revision]/ivy.xml"
		    artifactPattern "file://$buildDir/../../ivy-repository/[organisation]/[module]/[revision]/[artifact]-[revision](-[classifier]).[ext]"
	    }
    }
}

publish {
	dependsOn build
}

