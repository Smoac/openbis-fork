apply from: 'http://svncisd.ethz.ch/repos/cisd/gradle/trunk/javaproject.gradle'

group="sis"
buildInfo="jhdf5"

dependencies {
    compile 'cisd:cisd-args4j:+',
            'cisd:cisd-base:+',
            'rinn:restrictions:+'
    
    testCompile 'testng:testng:+',
                'jmock:jmock:+'
}

new ByteArrayOutputStream().withStream { os ->
    def result = exec {
        executable = 'svn'
        args = ['info']
        standardOutput = os
    }
    def outputAsString = os.toString()
    def matchLastChangedRevMatcher = outputAsString =~ /Last Changed Rev: (\d+)/
    def urlMatcher = outputAsString =~ /URL: .*\/(.+)/

    def revision = "r${matchLastChangedRevMatcher[0][1]}"
    def branch = urlMatcher[0][1]

    if (branch.equals('trunk')) {
        project.version = revision
    } else {
        project.version = branch
    }
}

task isClean(type: Exec) {
    workingDir = '.'
    commandLine = ['svn', 'status']
    standardOutput = new ByteArrayOutputStream()

    ext.output = { 
        return standardOutput.toString().length() == 0
    }    
}

task createBuildInfo(dependsOn: [isClean]){
    
    ext.output = "${buildDir}/BUILD-${buildInfo}.INFO"
    
    doLast {
        def writer = new FileWriter(ext.output)
        try {
            writer.println("SNAPSHOT:"+project.version.substring(1)+":"+(isClean.output()?"clean":"dirty"))
        } finally {
            writer.close()
        }
    }
}

tasks.withType(Test) {
    systemProperty "java.library.path", "libs/native/jhdf5/amd64-Linux:libs/native/jhdf5/amd64-Linux:libs/native/jhdf5/x86_64-Mac OS X:libs/native/jhdf5/x86_64-Mac OS X"
}

jar {
    baseName 'sis-jhdf5-core'
    from (sourceSets.main.output.classesDir) { 
        exclude('ch/systemsx/cisd/hdf5/h5ar') 
        exclude('ch/systemsx/cisd/hdf5/io')
    }
    from (createBuildInfo.output)
    
    manifest {
        attributes 'Main-Class': 'ch.systemsx.cisd.hdf5.BuildAndEnvironmentInfo'
        attributes 'Class-Path': configurations.runtime.collect { it.getName() }.join(' ')
    }
    
}
jar.dependsOn createBuildInfo

task nativeJarLinuxIntel(type: Jar) {
    archiveName 'hdf5-linux-intel.jar'
    from ("libs") {
        include 'native/jhdf5/amd64-Linux/**/*'
        include 'native/jhdf5/i386-Linux/**/*'
        include 'native/jhdf5/arm-Linux/**/*'
    }
    
    from (createBuildInfo.output)
}

task nativeJarMacIntel(type: Jar) {
    archiveName 'hdf5-macosx-intel.jar'
    from ("libs") {
        include 'native/jhdf5/i386-Mac OS X/**/*'
        include 'native/jhdf5/x86_64-Mac OS X/**/*'
    }
    
    from (createBuildInfo.output)
}

task nativeJarWindowsIntel(type: Jar) {
    archiveName 'hdf5-windows-intel.jar'
    from ("libs") {
        include 'native/jhdf5/amd64-Windows/**/*'
        include 'native/jhdf5/x86-Windows/**/*'
    }
    
    from (createBuildInfo.output)
}

task toolsJar(type: Jar, dependsOn: [compileJava]) {
    baseName 'sis-jhdf5-tools'
    from (sourceSets.main.output.classesDir) {
        include('ch/systemsx/cisd/hdf5/h5ar/**/*') 
        include('ch/systemsx/cisd/hdf5/io/**/*') 
    }
    from (createBuildInfo.output)    
    
    manifest {
        attributes 'Main-Class': 'ch.systemsx.cisd.hdf5.h5ar.HDF5ArchiverMain'
        attributes 'Class-Path': configurations.runtime.collect { it.getName() }.join(' ') + " sis-jhdf5-core.jar " + [nativeJarLinuxIntel, nativeJarMacIntel, nativeJarWindowsIntel].collect {it.archiveName}.join(' ')
    }
}

task allJar(type: Jar, dependsOn: [compileJava]) {
    baseName 'sis-jhdf5'

    from (sourceSets.main.output.classesDir) {
        include '**/*'
    }

    from (configurations.runtime.collect {zipTree(it)}) {
        exclude 'native/**/*'
        exclude 'BUILD-*.INFO'
    }
    from (createBuildInfo.output)
    
    manifest {
        attributes 'Main-Class': 'ch.systemsx.cisd.hdf5.h5ar.HDF5ArchiverMain'
        attributes 'Class-Path': [nativeJarLinuxIntel, nativeJarMacIntel, nativeJarWindowsIntel].collect {it.archiveName}.join(' ')
    }
}


task batteriesIncluded(type: Jar, dependsOn: [allJar, nativeJarLinuxIntel, nativeJarMacIntel, nativeJarWindowsIntel]) {
    archiveName 'sis-jhdf5-batteries_included.jar'
    from (zipTree(allJar.archivePath)) {
        include '**/*'
    }
    
    from (configurations.runtime.collect {zipTree(it)}) {
        include 'native/**/*'
    }

    from (zipTree(nativeJarLinuxIntel.archivePath)) {
        include 'native/**/*'
    }
    from (zipTree(nativeJarMacIntel.archivePath)) {
        include 'native/**/*'
    }
    from (zipTree(nativeJarWindowsIntel.archivePath)) {
        include 'native/**/*'
    }
    
    manifest {
        attributes 'Main-Class': 'ch.systemsx.cisd.hdf5.h5ar.HDF5ArchiverMain'
    }
}

task javadocZip(type: Zip, dependsOn: javadoc) {
    archiveName 'sis-jhdf5-javadoc.zip'
    from javadoc.destinationDir
}

task exampleZip(type: Jar) {
    archiveName 'sis-jhdf5-examples-src.zip'

    from ("sourceExamples/java") {
        include '**/*'
    }
    
    from ("targets/dist") {
        include "BUILD-jhdf5.INFO"
    }
}

task distributionZip(type: Zip, dependsOn: [jar, toolsJar, allJar, nativeJarLinuxIntel, nativeJarMacIntel, nativeJarWindowsIntel, batteriesIncluded, javadocZip, sourcesJar, exampleZip]) {
    baseName 'sis-jhdf5'

    from (jar.archivePath) {
        into 'sis-jhdf5/lib'
        rename '(.*)', 'sis-jhdf5-core.jar'
    }
    
    from (toolsJar.archivePath) {
        into 'sis-jhdf5/lib'
        rename '(.*)', 'sis-jhdf5-tools.jar'
    }

    from (allJar.archivePath) {
        into 'sis-jhdf5/lib'
        rename '(.*)', 'sis-jhdf5.jar'
    }
    
    from (configurations.runtime) {
        exclude '*restriction*'
        exclude '*bcel*'
        into 'sis-jhdf5/lib'
    }
    
    from (zipTree(batteriesIncluded.archivePath)) {
        into 'sis-jhdf5/lib/native'
        include 'native/**'
    }
    
    from (nativeJarLinuxIntel.archivePath) {
        into 'sis-jhdf5/lib/nativejar'
    }
    
    from (nativeJarMacIntel.archivePath) {
        into 'sis-jhdf5/lib/nativejar'
    }
    
    from (nativeJarWindowsIntel.archivePath) {
        into 'sis-jhdf5/lib/nativejar'
    }

    from (batteriesIncluded.archivePath) {
        into 'sis-jhdf5/lib/batteries_included'
    }

    from ("dist") {
        into 'sis-jhdf5'
    }
    
    from (javadocZip.archivePath) {
        into 'sis-jhdf5/doc'
    }
    
    from (exampleZip.archivePath) {
        into 'sis-jhdf5/doc'
    }    
    
    from (sourcesJar.archivePath) {
        into 'sis-jhdf5/src'
        rename '(.*)', 'sis-jhdf5-src.zip'
    }

	// BR 2014-06-09:
	// This is a workaround for an issue in Gradle: if one of entries from a jar file gets extraced read-only, 
	// overwriting it in a subsequent run will fail. It should be fixed in a future version of Gradle.
	// See http://forums.gradle.org/gradle/topics/ziptree_unable_to_overwrite_temporary_read_only_files 
    doLast {
        exec {
            commandLine = "chmod -R u+w $buildDir/tmp/expandedArchives".tokenize()
        }
    }

}

publishing {
    publications {
        artifacts(IvyPublication) {
            from components.java
            artifact(toolsJar) {
                name = "sis-jhdf5-tools"
                conf = "runtime"
            }
            artifact(nativeJarLinuxIntel) {
                name = "hdf5-linux-intel"
                conf = "runtime"
            }
            artifact(nativeJarMacIntel) {
                name = "hdf5-macosx-intel"
                conf = "runtime"
            }
            artifact(nativeJarWindowsIntel) {
                name = "hdf5-windows-intel"
                conf = "runtime"
            }
            artifact(sourcesJar) {
                type = "source"
            }            
        }
    }
}

build.dependsOn distributionZip
