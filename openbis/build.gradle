apply from: 'http://svncisd.ethz.ch/repos/cisd/gradle/trunk/javaproject.gradle'
apply plugin: 'war'

configurations {
	gwt
}

dependencies {

	compile (project(':common')) {
		transitive = false
	}

	compile (project(':openbis_api')) {
		transitive = false
	}

	compile (project(':openbis-common')) {
		transitive = false
	}

	compile (project(':authentication')) {
		transitive = false
	}

	compile (project(':dbmigration')) {
		transitive = false
	}

	compile 'cisd:cisd-base:r27861',
		'javax:servlet-api:3.0',
		'apache:log4j:1.2.15',
		'cisd:cisd-args4j:9.11.2',
		'springframework:spring-webmvc:2.5.6.SEC01-CISD',
		'apache:poi:3.8-20120326',
		'fasterxml:jackson-annotations:2.0.2',
		'python:jython:2.5.2',
		'sencha:gxt:2.2.5',
		'springframework:spring-aop:2.5.6.SEC01',
		'apache:commons-collections:4.01',
		'google:gwt-debug-panel:1.0',
		'javax:mail:1.4.3',
		'springframework:spring-jdbc:2.5.6.SEC01',
		'cisd:cisd-hotdeploy:13.01.0',
		'hibernate:hibernate-core:3.6.10.Final-CISD',
		'springframework:spring-orm:2.5.6.SEC01',
		'googlecode:jsonrpc4j:0.24-CISD',
		'lemnik:eodsql:2.2-CISD',
		'unimi:fastutil:5.1.5',
		'apache:commons-dbcp:1.3-CISD',
		'javax:validation-api:1.0.0.GA',
		'hibernate:hibernate-search:3.4.2.Final',
		'lucene:lucene-highlighter:3.1.0',
		'eclipse:jetty-util:8.1.8.v20121106',
		'hibernate:hibernate-validator:4.2.0.Final',
		'eclipse:jetty-deploy:8.1.8.v20121106',
		'apache:poi-ooxml:3.8-20120326'
		
	runtime (project(':common')) {
		transitive = true
	}

	runtime (project(':openbis_api')) {
		transitive = true
	}

	runtime (project(':openbis-common')) {
		transitive = true
	}

	runtime (project(':authentication')) {
		transitive = true
	}

	runtime (project(':dbmigration')) {
		transitive = true
	}
	
	testCompile (project(path: ':common', configuration: 'tests')) {
		transitive = false
	}

	testCompile (project(path: ':dbmigration', configuration: 'tests')) {
		transitive = false
	}
	
	testCompile 'springframework:spring-test:2.5.6.SEC01',
							'testng:testng:6.8-CISD',
							'hamcrest:hamcrest-library:1.1',
							'hamcrest:hamcrest-integration:1.1',
							'jmock:jmock:2.5.1'
				
	testRuntime (project(path: ':common', configuration: 'tests')) {
		transitive = true
	}

	testRuntime (project(path: ':dbmigration', configuration: 'tests')) {
		transitive = true
	}
										
	testRuntime 'fjelmer:classycle:1.4', 
		'apache:commons-fileupload:1.2.1'
		
	providedCompile 'google:gwt-user:2.4', 'google:gwt-dev:2.4'		
	
	gwt 'reveregroup:gwt-image-loader:1.1.4'
	
}

sourceSets { 
	test {
		resources {
			srcDirs = ['source/java']
		}
	}
}

// This task is here to make WebAppsPropertiesTest to work. It requires some data files
// to be present on the same directory than the class file itself.
task copyTestData(type: Copy, dependsOn: testClasses) {
    from 'sourceTest/java/ch/systemsx/cisd/openbis/generic/shared/basic'
    into 'out/classes/test/ch/systemsx/cisd/openbis/generic/shared/basic'
    include '*.properties'
}
test.dependsOn(copyTestData)

task compileGwt (dependsOn: classes, type: JavaExec) {

	buildDir = "${project.buildDir}/gwt"
	extraDir = "${project.buildDir}/extra"
     
	inputs.source sourceSets.main.java.srcDirs
	inputs.dir sourceSets.main.output.resourcesDir
	outputs.dir buildDir
     
	doFirst {
		file(buildDir).mkdirs()
	}
     
	main = 'com.google.gwt.dev.Compiler'
 
	classpath {
		[
			sourceSets.main.java.srcDirs,           // Java source
			sourceSets.main.output.resourcesDir,    // Generated resources
			sourceSets.main.output.classesDir,      // Generated classes
			sourceSets.main.compileClasspath,       // Deps
			project(":common").sourceSets.main.java.srcDirs,
			project(":openbis_api").sourceSets.main.java.srcDirs,
			configurations.gwt
		]   
	}
     
	args =
		[
			'ch.systemsx.cisd.openbis.OpenBIS', // Your GWT module
			'-war', buildDir,
			'-logLevel', 'INFO',
			'-localWorkers', '2',
			'-compileReport', 
			'-extra', extraDir,
		]
	
	maxHeapSize = '1024m'
}
 
war.dependsOn compileGwt
war {
    from compileGwt.buildDir
}
